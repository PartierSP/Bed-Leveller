' Gambas class file

Public iBedSizeX As Integer
Public iBedSizeY As Integer
Public iMargin As Integer
Public sInitialization As String
Public sHome As String
Public sRapidMove As String
Public iRapidPlane As Integer
Public sSetBedTemp As String
Public sBedTempPrefix As String
Public iBedTemp As Integer
Public fShim As Float
Public sPort As String
Public iBaud As Integer
Public iDataBits As Integer
Public iStopBits As Integer
Public sParity As String
Public sFlowControl As String
Public sConfigLoc As String = User.Home &/ ".config/leveller"
Public sConfigName As String = "config.conf"

Public Sub SerialPort3D_Read()

  Dim strReceived As String

  Try Read #SerialPort3D, strReceived, Lof(SerialPort3D)
  If Error Then
    strReceived = ""
  Else
    TextArea1.Text = TextArea1.Text & strReceived
  Endif

Catch
  Me.Text = Error.Text

End

Public Sub SendSerialData(strMessage As String, blnLF As Boolean)

  If blnLF Then
    strMessage &= gb.Lf
  Endif
  SerialPort3D.Begin()
  Write #SerialPort3D, strMessage
  SerialPort3D.Send()

Catch
  Me.Text = Error.Text

End

Public Sub Form_Open()

  Dim hSettings As Settings

  'Load settings
  hSettings = New Settings(sConfigLoc &/ sConfigName)

  iBedSizeX = hSettings["BedSize/BedSizeX", 235]
  iBedSizeY = hSettings["BedSize/BedSizeY", 240]
  ' 
  sInitialization = hSettings["Post/Init", "G21 G90 T0"]
  sHome = hSettings["Post/Home", "G28"]
  sRapidMove = hSettings["Post/RapidMove", "G0"]
  sSetBedTemp = hSettings["Post/SetBedTemp", "M140"]
  sBedTempPrefix = hSettings["Post/BedTempPrefix", "S"]
  
  iMargin = hSettings["Locations/Margin", 30]
  iRapidPlane = hSettings["Locations/RapidPlane", 10]
  fShim = hSettings["Locations/Shim", 0.2]
  iBedTemp = hSettings["Locations/BedTemp", 60]
  
  sPort = hSettings["Communications/Port", "/dev/ttyUSB0"]
  iBaud = hSettings["Communications/Baud", 115200]
  iDataBits = hSettings["Communications/DataBits", 8]
  iStopBits = hSettings["Communications/StopBits", 1]
  sParity = hSettings["Communications/Parity", "None"]
  sFlowControl = hSettings["Communications/FlowControl", "None"]
 'hSettings.Save
  hSettings.Read(Me, "MainForm")
  hSettings.Read(vsMain, "MainFormVSMain")

  With SerialPort3D
    .PortName = sPort
    .Speed = iBaud
    .DataBits = iDataBits
    .StopBits = iStopBits
    Select sParity
      Case "None"
        .Parity = SerialPort.None
      Case "Odd"
        .Parity = SerialPort.Odd
      Case "Event"
        .Parity = SerialPort.Even
    End Select
    Select sFlowControl
      Case "None"
        .FlowControl = SerialPort.None
      Case "Hardware"
        .FlowControl = SerialPort.Hardware
      Case "Software"
        .FlowControl = SerialPort.Software
      Case "Both"
        .FlowControl = SerialPort.Both
    End Select
  End With
  
  Try serialPort3D.Open()
  If Error Then
    Message.Warning("Printer not found.  Check settings/connection and try again.")
    FSettings.Show
  Else
    btnHome.Enabled = True
  Endif

End

Public Sub Form_Close()

  Dim hSettings As Settings

  'Save Window size/location settings
  hSettings = New Settings(sConfigLoc &/ sConfigName)
  hSettings["BedSize/BedSizeX"] = iBedSizeX
  hSettings["BedSize/BedSizeY"] = iBedSizeY

  hSettings["Post/Init"] = sInitialization
  hSettings["Post/Home"] = sHome
  hSettings["Post/RapidMove"] = sRapidMove
  hSettings["Post/SetBedTemp"] = sSetBedTemp
  hSettings["Post/BedTempPrefix"] = sBedTempPrefix

  hSettings["Locations/Margin"] = iMargin
  hSettings["Locations/RapidPlane"] = iRapidPlane
  hSettings["Locations/Shim"] = fShim
  hSettings["Locations/BedTemp"] = iBedTemp

   hSettings["Communications/Port"] = sPort
   hSettings["Communications/Baud"] = iBaud
   hSettings["Communications/DataBits"] = iDataBits
   hSettings["Communications/StopBits"] = iStopBits
   hSettings["Communications/Parity"] = sParity
   hSettings["Communications/FlowControl"] = sFlowControl

  hSettings.Write(Me, "MainForm")
  hSettings.Write(vsMain, "MainFormVSMain")

  'Turn off heated bed prior to exiting program
  SendSerialData(sSetBedTemp & sBedTempPrefix & "0.0", True)
  SerialPort3D.Close()

End

Public Sub btnHome_Click()

  'sHome Printer
  SendSerialData(Chr$(13), True)    'First communication sometimes results in error.  Sending a blank line lets this fault occure on this non-critial line.
  SendSerialData(sInitialization, True)
  SendSerialData(sHome, True)
  btLoc1.Enabled = True
  btnLoc2.Enabled = True
  btnLoc3.Enabled = True
  btnLoc4.Enabled = True
  btnLoc5.Enabled = True
  tgbBed.Enabled = True

End

Public Sub btLoc1_Click()

  'Move to -X,+Y position
  SendSerialData(sRapidMove & "Z" & iRapidPlane, True)
  SendSerialData(sRapidMove & "X" & iMargin & "Y" & (iBedSizeY - iMargin), True)
  SendSerialData(sRapidMove & "Z" & fShim, True)

End

Public Sub btnLoc2_Click()

  'Move to +X,+Y position
  SendSerialData(sRapidMove & "Z" & iRapidPlane, True)
  SendSerialData(sRapidMove & "X" & (iBedSizeX - iMargin) & "Y" & (iBedSizeY - iMargin), True)
  SendSerialData(sRapidMove & "Z" & fShim, True)

End

Public Sub btnLoc3_Click()

  'Move to -X,-Y position
  SendSerialData(sRapidMove & "Z" & iRapidPlane, True)
  SendSerialData(sRapidMove & "X" & iMargin & "Y" & iMargin, True)
  SendSerialData(sRapidMove & "Z" & fShim, True)

End

Public Sub btnLoc4_Click()

  'Move to +X,-Y position
  SendSerialData(sRapidMove & "Z" & iRapidPlane, True)
  SendSerialData(sRapidMove & "X" & (iBedSizeX - iMargin) & "Y" & iMargin, True)
  SendSerialData(sRapidMove & "Z" & fShim, True)

End

Public Sub btnSetting_Click()

  'Open Settings form
  FSettings.Show

End

Public Sub btnLoc5_Click()

  'Move to centre of the bed
  SendSerialData(sRapidMove & "Z" & iRapidPlane, True)
  SendSerialData(sRapidMove & "X" & (iBedSizeX / 2) & "Y" & (iBedSizeY / 2), True)
  SendSerialData(sRapidMove & "Z" & fShim, True)

End

Public Sub TextArea1_Change()

  TextArea1.Pos = Len(TextArea1.Text)

End

Public Sub tgbBed_Click()

  If tgbBed.Value == True Then
    'Turn on heated bed
    SendSerialData(sSetBedTemp & sBedTempPrefix & iBedTemp, True)
    tgbBed.Text = "Bed On"
  Else
    'Turn off heated bed
    SendSerialData(sSetBedTemp & sBedTempPrefix & "0.0", True)
    tgbBed.Text = "Bed Off"
  Endif

End
